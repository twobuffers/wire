import buildcfg.Deps
import buildcfg.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    // STEP 1: enable google services plugin (to process google-services.json)
    id 'com.google.gms.google-services'
}

kapt {
    correctErrorTypes = true
}

android {
    compileSdkVersion AndroidConfig.COMPILE_SDK_VERSION
    buildToolsVersion AndroidConfig.BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.twobuffers.wire.sample_firebase_messaging"
        minSdkVersion AndroidConfig.MIN_SDK_VERSION
        targetSdkVersion AndroidConfig.TARGET_SDK_VERSION
        versionCode 1000001
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = Versions.JAVA_VERSION_STR
        targetCompatibility = Versions.JAVA_VERSION_STR
    }
}

dependencies {
    implementation project(':wire-di-annotations')     // ActivityScoped
    implementation project(':wire-coroutines-android') // ProcessLifetimeCoroutineScope
    implementation project(':wire-utils')          // className
    // STEP 2: add wire libraries
    implementation project(':wire-initializer')    // Initializers
    implementation project(':wire-firebase-messaging')

    implementation(Deps.OrgJetBrains.KotlinX.Coroutines.kotlinxCoroutinesCore)

    implementation(Deps.ComGoogle.Dagger.dagger)
    implementation(Deps.ComGoogle.Dagger.daggerAndroid)
    implementation(Deps.ComGoogle.Dagger.daggerAndroidSupport)
    kapt(Deps.ComGoogle.Dagger.daggerCompiler)
    kapt(Deps.ComGoogle.Dagger.daggerAndroidProcessor)

    implementation(Deps.AndroidX.appcompat)
    implementation(Deps.AndroidX.constraintlayout)
    implementation(Deps.AndroidX.recyclerview)
    implementation(Deps.AndroidX.Lifecycle.lifecycleRuntimeKtx) // androidx.lifecycle.lifecycleScope

    implementation project(':wire-epoxy')
    kapt(Deps.ComAirbnbAndroid.EpoxyBeta.epoxyProcessor)
}
